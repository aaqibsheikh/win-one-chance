// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@chainlink/contracts/src/v0.8/VRFConsumerBase.sol";

contract WinOneChance is VRFConsumerBase, Ownable {

    //chainlink  variables
    // The amount of LINK to send with the request
    uint256 public fee;

    // ID of public key against which randomness is generated
    bytes32 public keyHash;

    // Address of the players
    address[] public players;

    // Max number of players in one game
    uint8 public maxPlayers;

    // isgame started or not
    bool public gameStarted;

    // fee for entering in game
    uint256 public entryFee;

    // current gameId
    uint256 public gameId;

    // emit when game start
    event GameStarted(uint256 gameId, uint8 maxPlayers, uint256 entryFee);
    // emit when someone join game
    event PlayerJoined(uint256 gameId, address player);
    // emit when game ends
    event GameEnd(uint256 gameId, address winnner, bytes32 requestId);

    // constructor
    constructor(address vrfCoordinator, address linkToken, bytes32 vrfKeyHash, uint256 vrfFee) VRFConsumerBase(vrfCoordinator, linkToken) {
        keyHash = vrfKeyHash;
        fee = vrfFee;
        gameStarted = false;
    }

    /**
    * startGame starts the game by setting appropriate values for all the variables
    */

    function startGame(uint8 _maxPlayers, uint256 _entryFee) public onlyOwner {
        require(!gameStarted, "Game is currently running");
        delete players;
        maxPlayers = _maxPlayers;
        entryFee = _entryFee;
        gameStarted = true;
        gameId += 1;
        emit GameStarted(gameId, maxPlayers, entryFee);
    }

    /**
    joinGame is called when a player wants to enter the game
     */
    function joinGame() public payable {
        require(gameStarted, "Game has not been started yet");
        require(msg.value == entryFee, "Value sent is not equal to entryFee");
        require(players.length < maxPlayers, "Game is full");
        players.push(msg.sender);
        emit PlayerJoined(gameId, msg.sender);

        if(players.length == maxPlayers) {
            getRandomWinner();
        }
    }

    /**
    * getRandomWinner is called to start the process of selecting a random winner
    */
    function getRandomWinner() private returns (bytes32 requestId) {
        // LINK is an internal interface for Link token found within the VRFConsumerBase
        // Here we use the balanceOF method from that interface to make sure that our
        // contract has enough link so that we can request the VRFCoordinator for randomness
        require(LINK.balanceOf(address(this)) >= fee, "Not enough LINK");

        // Make a request to the VRF coordinator.
        // requestRandomness is a function within the VRFConsumerBase
        // it starts the process of randomness generation
        return requestRandomness(keyHash, fee);
    }

    /**
    * fulfillRandomness is called by VRFCoordinator when it receives a valid VRF proof.
    * This function is overrided to act upon the random number generated by Chainlink VRF.
    * @param requestId  this ID is unique for the request we sent to the VRF Coordinator
    * @param randomness this is a random unit256 generated and returned to us by the VRF Coordinator
   */
   function fulfillRandomness(bytes32 requestId, uint256 randomness) internal virtual override {
        // We want out winnerIndex to be in the length from 0 to players.length-1
        // For this we mod it with the player.length value
        uint256 winnerIndex = randomness % players.length;

        // get the address of the winner from the players array
        address winner = players[winnerIndex];

        // send the ether in the contract to the winner
        (bool sent,) = winner.call{value: address(this).balance}("");

        require(sent, "Failed to sent ether");

        // emit game ended event
        emit GameEnd(gameId, winner, requestId);
        gameStarted = false;
   }
}